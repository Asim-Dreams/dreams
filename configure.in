#
#  Copyright (C) 2003-2006 Intel Corporation
#  
#  This program is free software; you can redistribute it and/or
#  modify it under the terms of the GNU General Public License
#  as published by the Free Software Foundation; either version 2
#  of the License, or (at your option) any later version.
#  
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#  
#  You should have received a copy of the GNU General Public License
#  along with this program; if not, write to the Free Software
#  Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
#  
#

dnl Process this file with autoconf to produce a configure script.
dnl This is the configure.in file for asim-dreams.

AC_INIT([ASIM dreams], [1.3a], [eric.borch@intel.com], [asim-dreams])

dnl required autoconf version
AC_PREREQ(2.61) 

dnl Use scripts in ./scripts
AC_CONFIG_AUX_DIR(scripts)

dnl AC_CONFIG_SRCDIR([admin/packages/dreams])

dnl automake version + options
AM_INIT_AUTOMAKE([1.8.3 foreign subdir-objects]) 
AM_CONFIG_HEADER(config.h)

dnl prevent casual installer from having to deal with Makefile.in etc.
dnl getting regenerated; this could happen due to timestamp issues of
dnl the files coming out of the repository;
AM_MAINTAINER_MODE

dnl Installation defaults
dnl AC_PREFIX_DEFAULT(/usr/local)
AC_PREFIX_PROGRAM(asimstarter)




dnl
dnl FIND_QT(<dirs>)
dnl

AC_DEFUN(FIND_MOC,[
    AC_MSG_CHECKING("for Qt moc")

    for dir in $1
    do
        if test -f $dir/moc
        then
            QTMOCDIR=$dir
            AC_MSG_RESULT("found in $QTMOCDIR")
            break
        else
            if test -f $dir/bin/moc
            then
                QTMOCDIR=$dir/bin
                AC_MSG_RESULT("found in $QTMOCDIR")
                break
            fi
        fi
    done
])

AC_DEFUN(FIND_UIC,[
    AC_MSG_CHECKING("for Qt uic")

    for dir in $1
    do
        if test -f $dir/uic
        then
            QTUICDIR=$dir
            AC_MSG_RESULT("found in $QTUICDIR")
            break
        else
            if test -f $dir/bin/uic
            then
                QTUICDIR=$dir/bin
                AC_MSG_RESULT("found in $QTUICDIR")
                break
            fi
        fi
    done
])

AC_DEFUN(FIND_QT_LIBS,[
    AC_MSG_CHECKING("for Qt 3.x.x libraries")

    QTLIBS="qt-mt qt"

    for dir in $1
    do
        for i in $QTLIBS
        do
            if test -f $dir/lib$i.a -o -f $dir/lib$i.so
            then
                QTLIBDIR=$dir
                AC_MSG_RESULT("found in $QTLIBDIR")
                QT=$i
                break 2
            else
                if test -f $dir/lib/lib$i.a -o -f $dir/lib/lib$i.so
                then
                    QTLIBDIR=$dir/lib
                    AC_MSG_RESULT("found in $QTLIBDIR")
                    QT=$i
                    break 2
                fi
            fi
        done
    done
])

AC_DEFUN(FIND_QT_INCLUDES,[
    AC_MSG_CHECKING("for Qt 3.x.x include files")

    for dir in $1
    do
        if test -f $dir/qwidget.h
        then
            QTINCDIR=$dir
            AC_MSG_RESULT("found in $QTINCDIR")
            break
        else
            if test -f $dir/include/qwidget.h
            then
                QTINCDIR=$dir/include
                AC_MSG_RESULT("found in $QTINCDIR")
                break
            fi
        fi
    done
])

AC_DEFUN(FIND_QT, [
    qt_available=1;

    AC_SUBST(QT)

    FIND_MOC($with_qt_moc $with_qt_dirx $QTDIR /usr/intel/pkgs/libqt/3.3.4 ${prefix}/lib/qt /usr/lib/qt3 /usr/local/qt /usr/lib/qt /usr/bin /bin /usr/local /usr/X11R6)
    if test "X$QTMOCDIR" = "X"; then
        echo ""
        echo "Qt's moc not found! If you have installed Qt in an"
        echo "unusual place, please use the \"--with-qt-moc=\"" option
        echo ""
        echo "****************************************************************"
        echo "****************************************************************"
        echo "You don't have QT Library available in this system. Remember"
        echo "you cannot compile 2Dreams"
        echo "****************************************************************"
        echo "****************************************************************"
        echo ""
        qt_available=0;
    fi

    dnl
    dnl find uic
    dnl

    FIND_UIC($with_qt_uic $with_qt_dir $QTDIR /usr/intel/pkgs/libqt/3.3.4 ${prefix}/lib/qt /usr/lib/qt3 /usr/local/qt /usr/lib/qt /usr/bin /bin /usr/local /usr/X11R6)
    if test "X$QTUICDIR" = "X"; then
        echo ""
        echo "Qt's uic not found! If you have installed Qt in an"
        echo "unusual place, please use the \"--with-qt-uic=\"" option
        echo ""
        echo "****************************************************************"
        echo "****************************************************************"
        echo "You don't have QT Library available in this system. Remember"
        echo "you cannot compile 2Dreams"
        echo "****************************************************************"
        echo "****************************************************************"
        echo ""
        qt_available=0;
    fi

    dnl
    dnl find qt libraries
    dnl

    FIND_QT_LIBS($with_qt_libs $with_qt_dir $QTDIR /usr/intel/pkgs/libqt/3.3.4/lib ${prefix}/lib/qt /usr/lib/qt3 /usr/local/qt /usr/lib/qt /usr/lib /usr/local/lib /usr/X11R6/lib)
    if test "X$QTLIBDIR" = "X"
    then
        echo ""
        echo "Qt libs not found! If you have installed Qt in an"
        echo "unusual place, please use the \"--with-qt-libs=\"" option
        echo ""
        echo "****************************************************************"
        echo "****************************************************************"
        echo "You don't have QT Library available in this system. Remember"
        echo "you cannot compile 2Dreams"
        echo "****************************************************************"
        echo "****************************************************************"
        echo ""
        qt_available=0;
    else
        QTLIBOBJ="-l$QT"
        QTLIBDIR="-L$QTLIBDIR"
    fi

    dnl 
    dnl find qt headers
    dnl

    FIND_QT_INCLUDES($with_qt_includes $with_qt_dir $QTDIR /usr/intel/pkgs/libqt/3.3.4 ${prefix}/lib/qt /usr/lib/qt3 /usr/local/qt /usr/lib/qt /usr/include /usr/include/qt3 /usr/include/qt /usr/local/include /usr/X11R6/include/qt3)
    if test "X$QTINCDIR" = "X"
    then
        echo ""
        echo "Qt headers not found! If you have installed Qt in an"
        echo "unusual place, please use the \"--with-qt-includes=\"" option
        echo ""
        echo "****************************************************************"
        echo "****************************************************************"
        echo "You don't have QT Library available in this system. Remember"
        echo "you cannot compile 2Dreams"
        echo "****************************************************************"
        echo "****************************************************************"
        echo ""
        qt_available=0;
    else
        QTINCDIR="-I$QTINCDIR"
    fi    
])

AC_DEFUN(FIND_GUI, [

    AC_PATH_XTRA

    FIND_QT

    if test $qt_available = 1 
    then
        AC_SUBST(QTINCDIR)
        AC_SUBST(QTLIBDIR)
        AC_SUBST(QTLIBOBJ)
        AC_SUBST(QTUICDIR)
        AC_SUBST(QTMOCDIR)

        dnl Configuring AGT defines

        case $host in
        alpha*) 
       	    X_EXTRA_LIBS="-lXext -lX11" ;;
        i?86*)
	    X_EXTRA_LIBS="-ldl -lGL -lXext -lX11 -lXinerama -lXrender" ;;
        x86_64*)
	    X_EXTRA_LIBS="-ldl -lGL -lXext -lX11 -lXinerama -lXrender" ;;
        *)
            echo "This architecture is not supported" ;
            exit ;;
        esac
    
        AC_SUBST(X_EXTRA_LIBS)
    fi

])

AC_DEFUN(FIND_ASIM_CORE_DIR,[
    AC_MSG_CHECKING("for Asim core directory")

    TLIBS="agt dral draldb"

    for i in $TLIBS
    do
        if test "X$1" = "X"
        then
            ASIMCORELIBS="$ASIMCORELIBS -l$i"
            AC_MSG_RESULT("lib$i.a not found will look in $prefix")
        else
            for dir in $1
            do
                if test -f $dir/lib/lib$i/lib$i.a
                then
                    LOCAL_ASIMCOREINCS="$LOCAL_ASIMCOREINCS -I$dir/lib/lib$i/include/"
                    LOCAL_ASIMCORELIBDIRS="$LOCAL_ASIMCORELIBDIRS $dir/lib/lib$i/lib$i.a"
                    AC_MSG_RESULT("lib$i.a found in $dir")
                else
                    ASIMCORELIBS="$ASIMCORELIBS -l$i"
                    AC_MSG_RESULT("lib$i.a not found will look in $prefix")
                fi
            done
        fi
    done

    dnl
    dnl draldb is only required by 2Dreams. By now Dreams has its own modifyed
    dnl version of the library
    dnl
    TWODREAMSLIBS=""
    i="draldb"
    if test "X$1" = "X"
    then
        TWODREAMSLIBS="-l$i"
        AC_MSG_RESULT("lib$i.a not found will look in $prefix")
    else
        for dir in $1
        do
            if test -f $dir/lib/lib$i/lib$i.a
            then
                LOCAL_2DREAMSINCS="-I$dir/lib/lib$i/include/"
                LOCAL_2DREAMSLIBDIRS="$dir/lib/lib$i/lib$i.a"
                AC_MSG_RESULT("lib$i.a found in $dir")
            else
                TWODREAMSLIBS="-l$i"
                AC_MSG_RESULT("lib$i.a not found will look in $prefix")
            fi
        done
    fi
])

AC_DEFUN(FIND_ASIM_DIR,[
    AC_MSG_CHECKING("for Asim directory")

    for dir in $1
    do
        if test -f $dir/bin/asimstarter
        then
            GLOBAL_ASIMCOREINCS="$GLOBAL_ASIMCOREINCS -I$dir/include/"
            GLOBAL_ASIMCORELIBDIRS="$GLOBAL_ASIMCORELIBDIRS -L$dir/lib"
            AC_MSG_RESULT("found in $dir")
        fi
    done
])

dnl
dnl with arguments
dnl

AC_ARG_WITH(qt-dir,        [  --with-qt-dir=DIR          qt-dir in DIR])
AC_ARG_WITH(qt-moc,        [  --with-qt-moc=DIR          qt-moc in DIR])
AC_ARG_WITH(qt-uic,        [  --with-qt-uic=DIR          qt-uic in DIR])
AC_ARG_WITH(qt-includes,   [  --with-qt-includes=DIR     qt-includes in DIR])
AC_ARG_WITH(qt-libs,       [  --with-qt-libs=DIR         qt-libs in DIR])
AC_ARG_WITH(asim-core-dir, [  --with-asim-core-dir=DIR   asim-core-dir in DIR])
AC_ARG_WITH(asim-dir,      [  --with-asim-dir=DIR        asim-dir in DIR])


dnl
dnl --enable-distcc
dnl
AC_ARG_ENABLE([distcc],
      AC_HELP_STRING([--enable-distcc=ARG],
                     [use distcc compiler [[ARG=no|yes]] (default=no)]),
      asim_cv_enable_distcc=$enableval,
      asim_cv_enable_distcc=no)

AC_CACHE_CHECK([if distcc compiler should be used] ,
      asim_cv_enable_distcc,
      asim_cv_enable_distcc=no)

dnl
dnl compile switches
dnl
dnl --enable-warning
dnl
AC_ARG_ENABLE([warning],
      AC_HELP_STRING([--enable-warning=ARG],
                     [warning level [[ARG=no|warn|error]] (default=warn)]),
      asim_cv_enable_warning=$enableval,
      asim_cv_enable_warning=warn)

AC_CACHE_CHECK([which warning level to use],
      asim_cv_enable_warning,
      asim_cv_enable_warning=warn)

if test $asim_cv_enable_warning = yes; then
    asim_cv_enable_warning=warn
fi
if test $asim_cv_enable_warning = no; then
    WARNFLAGS="-Wformat"
fi
if test $asim_cv_enable_warning = warn -o $asim_cv_enable_warning = error; then
    WARNFLAGS="-Wall -W -Wshadow -Wcast-align -Wcast-qual -Wold-style-cast -Wredundant-decls -Wnon-virtual-dtor -ansi"
fi
if test $asim_cv_enable_warning = error; then
    WARNFLAGS="${WARNFLAGS} -Werror"
fi
AC_SUBST(WARNFLAGS)

dnl
dnl --enable-optimize
dnl
AC_ARG_ENABLE([optimize],
      AC_HELP_STRING([--enable-optimize=ARG],
                     [optimizations [[ARG=no|yes|high]] (default=yes)]),
      asim_cv_enable_optimize=$enableval,
      asim_cv_enable_optimize=yes)

AC_CACHE_CHECK([if optimizations should be used] ,
      asim_cv_enable_optimize,
      asim_cv_enable_optimize=yes)

if test $asim_cv_enable_optimize = no; then
    OPTFLAGS="-g -O0 -fno-inline -DQT_DEBUG -DQT_FATAL_ASSERT"
fi
if test $asim_cv_enable_optimize = yes; then
    OPTFLAGS="-O2 -DQT_DEBUG -DQT_FATAL_ASSERT"
fi
if test $asim_cv_enable_optimize = high; then
    OPTFLAGS="-O3 -finline-limit=1200 -funroll-loops -fexpensive-optimizations -frerun-loop-opt -DQT_NO_DEBUG -DQT_NO_CHECK"
fi
AC_SUBST(OPTFLAGS)

dnl pull all flags together
AM_CXXFLAGS='$(WARNFLAGS) $(OPTFLAGS) $(COREFLAGS) $(COREINCS) $(ASIMCORE_CFLAGS)'
AM_LDFLAGS='$(CORELIBS) $(ASIMCORE_LFLAGS)'
AC_SUBST(AM_CXXFLAGS)
AC_SUBST(AM_LDFLAGS)

dnl This is the asim package name
AC_SUBST(package)
package=dreams

dnl This is the release tag out of the package file.
dnl The tag is assumed to be in a line that looks like: Tag=<tagname>
AC_SUBST(release)
release=`grep '^Tag=' ./admin/packages/$package | sed 's/Tag=//'`


dnl This is the directory for configuration data
configdir='${sysconfdir}/asim/${release}'
AC_SUBST(configdir)


dnl This is where architecture independent info is placed
packagedir='${datadir}/asim'
AC_SUBST(packagedir)

dnl This is where a copy of the package is placed
codedir='${packagedir}/${package}/${release}'
AC_SUBST(codedir)


dnl This is where architecture dependent info is placed
tooldir='${libexecdir}/asim/${release}'
AC_SUBST(tooldir)


dnl Checks for programs.
if test x${CXXFLAGS+set} = x; then
    dnl prevent AC_PROG_CXX from setting CXXFLAGS to -O -g flags
    CXXFLAGS=""
fi


dnl check if we should use distcc or not

if test $asim_cv_enable_distcc = "yes"; then
    distcc="distcc gcc"
    distcxx="distcc g++"
else
    distcc=""
fi

AC_PROG_CC("$distcc" gcc cc)
AC_PROG_CXX("$distcxx" g++ cxx)

AC_PROG_LN_S
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_RANLIB
dnl tools for building libraries
AM_DISABLE_SHARED
AM_PROG_LIBTOOL

#dnl Require lex and yacc
# Sometimes Flex is installed as Lex, e.g., NetBSD.
#AC_CHECK_PROG([FLEX], [flex lex], [flex])
# Force the use of `missing' to wrap Flex invocations.
#AM_MISSING_PROG([LEX], [$FLEX])
# Perform all the tests Automake and Autoconf need.
#AM_PROG_CC_C_O

AM_PROG_LEX
AC_PROG_YACC


dnl Check for perl version

AC_MSG_CHECKING(for a perl version >= 5.6)
perlok=`perl -e 'if ("$]" >= 5.006) { print "yes\n";} else {print "no\n"}'`
if test "$perlok" = "yes" ; then
    AC_MSG_RESULT(yes)
else
    AC_MSG_ERROR(Wrong perl version - need at least version 5.6)
fi

dnl Check for perl packages
dnl TBD

dnl Check for tcl version
dnl TBD

dnl Handle gui-based stuff

AC_ARG_ENABLE([gui],
      AC_HELP_STRING([--enable-gui=ARG],
         [build gui-based programs [[ARG=no|yes]] (default=yes)]),
      asim_cv_enable_gui=$enableval,
      asim_cv_enable_gui=yes)

if test "$asim_cv_enable_gui" = "yes" 
then
    NO_GUI=''
    FIND_GUI()
else
    NO_GUI='#'
    qt_available=0
fi
AC_SUBST(NO_GUI)

asimcore_dir=`awb-resolver admin/packages/asimcore | sed 's/admin\/packages\/asimcore//'`

dnl check for asimcore
FIND_ASIM_CORE_DIR($with_asim_core_dir `dirname $PWD`/asim-simcore $asimcore_dir)
FIND_ASIM_DIR($with_asim_dir /usr /usr/local $prefix)
ASIMCORE_CFLAGS="$LOCAL_ASIMCOREINCS $GLOBAL_ASIMCOREINCS"
ASIMCORE_LFLAGS="$LOCAL_ASIMCORELIBDIRS $GLOBAL_ASIMCORELIBDIRS $ASIMCORELIBS"
TWODREAMS_CFLAGS="$LOCAL_2DREAMSINCS"
TWODREAMS_LFLAGS="$LOCAL_2DREAMSLIBDIRS $TWODREAMSLIBS"
TDREAMS_CFLAGS="-I./Loki-Lib/ -I/usr/include/libxml2/"
AC_SUBST(TWODREAMS_CFLAGS)
AC_SUBST(TWODREAMS_LFLAGS)
AC_SUBST(ASIMCORE_CFLAGS)
AC_SUBST(ASIMCORE_LFLAGS)
AC_SUBST(TDREAMS_CFLAGS)

dnl Define LINK... 

LINK="ar -r"
AC_SUBST(LINK)


dnl
dnl some glob libraries do not support tilde and brace expansion
dnl
AC_MSG_CHECKING(for define of GLOB_TILDE)
AC_TRY_COMPILE(
  [#include <glob.h>],
  [int foo = GLOB_TILDE;],
  have_glob_brace=1,
  have_glob_brace=0
)
if test $have_glob_brace = 1 ; then
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
  AC_MSG_WARN(No ~ substitution available for filename paths)
  AC_DEFINE(GLOB_TILDE, 0,
    [stub out GLOB_TILDE if not defined by system headers])
fi
AC_MSG_CHECKING(for define of GLOB_BRACE)
AC_TRY_COMPILE(
  [#include <glob.h>],
  [int foo = GLOB_BRACE;],
  have_glob_brace=1,
  have_glob_brace=0
)
if test $have_glob_brace = 1 ; then
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
  AC_MSG_WARN(No {} substitution available for filename paths)
  AC_DEFINE(GLOB_BRACE, 0,
    [stub out GLOB_BRACE if not defined by system headers])
fi

dnl
dnl Some defines from the ASIM core to be able to include syntax.h
dnl (This is code is borrowed and Bourne-shellified from Makefile.config)
dnl (... and its quite ugly ...)
dnl
HOSTOS=`uname -s`
HOSTMACHINE=`uname -m`
HOSTOSRELEASE=`uname -r`
HOSTOSRELEASE_SUBST=`echo $HOSTOSRELEASE | tr ".-" "__"`

if test $HOSTOS = "OSF1" ; then
  AC_DEFINE(HOST_DUNIX, 1,
    [system is DigitalUnix Alpha])
  if test $GNU != 1 ; then
    CORELIBS="-L/proj/vssad/local/alpha/lib"
    # cxx on Tru64 needs this to compile standard iostream right
    COREFLAGS="-DHOST_DUNIX -D__USE_STD_IOSTREAM -DQT_CLEAN_NAMESPACE -DQT_NO_STL -DQ_STRICT_INLINING_RULES"
    COREINCS="-I/usr/local/include"
    AC_DEFINE(__USE_STD_IOSTREAM, 1,
      [convince DigitalUnix CXX to follow C++ IOstream standard])
  else
    CORELIBS="-L/proj/vssad/local/alpha/lib"
    COREINCS="-idirafter /usr/local/include"
    COREFLAGS="-DHOST_DUNIX -DQT_CLEAN_NAMESPACE -DQT_NO_STL -DQ_STRICT_INLINING_RULES"
  fi
  AC_SUBST(COREFLAGS)
  AC_SUBST(CORELIBS)
  AC_SUBST(COREINCS)
else
  if test $HOSTOS = "FreeBSD" ; then
    case $HOSTMACHINE in
      i?86) 
        COREFLAGS="-DHOST_FREEBSD_X86" ;
        AC_DEFINE(HOST_FREEBSD_X86, 1, [system is FreeBSD x86]) ;;
      *)    
        COREFLAGS="-DHOST_FREEBSD" ;
        AC_DEFINE(HOST_FREEBSD, 1, [system is FreeBSD (other)]) ;;
    esac
    
    CORELIBS="-L/usr/local/lib -L/usr/X11R6/lib" ;
    COREINCS="-L/usr/local/lib -L/usr/X11R6/lib" ;
  else
    AC_DEFINE(HOST_LINUX, 1, [system is Linux])
dnl can't use AC_DEFINE for the following, since autoheader does not
dnl support variables in the first argument of AC_DEFINE
    case $HOSTMACHINE in
      i?86) 
        COREFLAGS="-DQT_CLEAN_NAMESPACE -DQT_NO_STL -DQ_STRICT_INLINING_RULES -DHOST_LINUX_X86 -DLINUX_$HOSTOSRELEASE_SUBST -D__STDC_LIMIT_MACROS" ;
        CORELIBS="-L/usr/X11R6/lib"
        COREINCS="-idirafter /usr/local/include" ;
        AC_DEFINE(HOST_LINUX_X86, 1, [system is Linux x86]) ;;
      x86_64) 
        COREFLAGS="-DQT_CLEAN_NAMESPACE -DQT_NO_STL -DQ_STRICT_INLINING_RULES -DHOST_LINUX_X86 -DLINUX_$HOSTOSRELEASE_SUBST -D__STDC_LIMIT_MACROS" ;
        CORELIBS="-L/usr/X11R6/lib"
        COREINCS="-idirafter /usr/local/include" ;
        AC_DEFINE(HOST_LINUX_X86, 1, [system is Linux x86]) ;;

      ia64) 
        COREFLAGS="-DQT_CLEAN_NAMESPACE -DQT_NO_STL -DQ_STRICT_INLINING_RULES -DHOST_LINUX_IA64 -DLINUX_$HOSTOSRELEASE_SUBST" ;
        CORELIBS="-L/usr/X11R6/lib" ;
        COREINCS="-idirafter /usr/local/include" ;
        AC_DEFINE(HOST_LINUX_IA64, 1, [system is Linucx IA64]) ;;
    esac
  fi
  AC_SUBST(COREFLAGS)
  AC_SUBST(CORELIBS)
  AC_SUBST(COREINCS)
fi

dnl Checks for libraries.
AC_CHECK_LIB(popt, poptGetContext)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_MODE_T
AC_TYPE_SIZE_T
AC_TYPE_SIGNAL
AC_STRUCT_TM

dnl Checks for library functions.
AC_CHECK_FUNCS(getcwd mkdir regcomp rmdir strdup strerror strtol strtoul)

dnl setup all files that need to be generated
dnl top level
AC_CONFIG_FILES(Makefile)
dnl tools
AC_CONFIG_FILES(tools/Makefile)

if test $qt_available = 1; then
    dnl 2Dreams
    AC_CONFIG_FILES(tools/Dreams/Makefile tools/Dreams/src/Makefile)
    dnl AC_CONFIG_FILES(tools/2Dreams/Makefile tools/2Dreams/src/Makefile)
    AC_CONFIG_FILES(tools/1Dream/Makefile tools/1Dream/src/Makefile)
    AC_CONFIG_FILES(tools/TDreams/Makefile tools/TDreams/src/Makefile)
    AC_CONFIG_FILES(tools/vcgDreams/Makefile tools/vcgDreams/vcgen/Makefile tools/vcgDreams/vcgen/src/Makefile )
    AC_CONFIG_FILES(tools/stc2stb/Makefile tools/stc2stb/src/Makefile)
    AC_CONFIG_FILES(tools/stb2stc/Makefile tools/stb2stc/src/Makefile)
    AC_CONFIG_FILES(tools/dralTranslator/Makefile tools/dralTranslator/src/Makefile)
    AC_CONFIG_FILES(tools/dral2PTV/Makefile tools/dral2PTV/src/Makefile)
else
    echo ""
    echo "*******************************************************************"
    echo "*******************************************************************"
    echo "You don't have the QT Library configured. Therefore,"
    echo "1Dream and 2Dreams will not be compiled"
    echo "*******************************************************************"
    echo "*******************************************************************"
    echo ""
fi


dnl now generate all files
AC_OUTPUT
